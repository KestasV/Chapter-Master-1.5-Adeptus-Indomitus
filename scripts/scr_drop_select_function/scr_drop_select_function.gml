enum eDROP_TYPE {
    RAIDATTACK = 0,
    PURGESELECT,
    PURGEBOMBARD,
    PURGEFIRE,
    PURGESELECTIVE,
    PURGEASSASSINATE,
}

/// @self Asset.GMObject.obj_drop_select
function drop_select_unit_selection() {
    w = 720;
    h = 580;
    // Center of the screen
    var _x_center = main_slate.XX;
    var _y_center = main_slate.YY;
    var x1 = _x_center;
    var y1 = _y_center;
    var x2 = x1 + w;
    var y2 = y1 + h;
    var x3 = (x1 + x2) / 2;

    if (purge == eDROP_TYPE.RAIDATTACK) {
        draw_set_font(fnt_40k_30b);
        draw_set_halign(fa_left);
        draw_set_color(CM_GREEN_COLOR);
        var attack_type = attack ? "Attacking" : "Raiding";
        draw_text_transformed(x1 + 40, y1 + 38, $"{attack_type} ({planet_numeral_name(planet_number, p_target)} )", 0.6, 0.6, 0);
        var _offset = x1 + 40;
        draw_set_font(fnt_40k_14);
        for (var i = 0; i < array_length(roster.company_buttons); i++) {
            var _button = roster.company_buttons[i];
            _button.x1 = _offset;
            _button.y1 = y1 + 70;
            _button.update();
            _button.draw();
            if (_button.company_present) {
                if (_button.clicked()) {
                    roster.update_roster();
                }
            }
            _offset += _button.w + 8;
        }

        // Planet icon here
        // draw_rectangle(xx+1084,yy+215,xx+1142,yy+273,0);

        // Formation
        // Hardening: never index formation_possible without checking it is non-empty and the
        // index is in range. A drifted or stale formation_current (e.g. across a load) would
        // otherwise crash the whole drop screen on draw.
        var _formation_str = "Formation: -";
        if (array_length(formation_possible) > 0) {
            formation_current = clamp(formation_current, 0, array_length(formation_possible) - 1);
            _formation_str = $"Formation: {obj_controller.bat_formation[formation_possible[formation_current]]}";
        }
        // Upstream renamed this button to btn_formation in obj_drop_select Create_0;
        // the old name here crashed the whole attack screen on draw.
        btn_formation.x1 = x2 - 40 - (string_width(_formation_str) + 4);
        btn_formation.y1 = y1 + 80;
        btn_formation.update({str1: _formation_str});
        btn_formation.draw();
        if (btn_formation.clicked()) {
            if (array_length(formation_possible) > 0) {
                formation_current++;
                if (formation_current >= array_length(formation_possible)) {
                    formation_current = 0;
                }
            }
        }

        // Ships Are Up, Fuck Me
        draw_set_color(CM_GREEN_COLOR);
        draw_text(x1 + 40, 273, "Available Forces:");
    }

    var _buttons_x = x1 + 40;
    var _buttons_y = 299;

    roster.select_all_ships.update({x1: x1 + 200, y1: 273});
    if (roster.select_all_ships.draw()) {
        roster.ship_multi_selector.select_all();
    }

    // Local force button;
    if (purge != eDROP_TYPE.PURGEBOMBARD) {
        var _local_button = roster.local_button;
        // Local force exhaustion: planetside forces also support at most
        // SHIP_ASSAULTS_PER_TURN ground assaults per turn, closing the loop of
        // deploying troops to the surface and attacking endlessly for free. When
        // spent, the button locks red like an exhausted ship. Attacks only.
        var _locals_spent = (attack == 1) && (local_assaults_used(p_target, planet_number) >= GROUND_ASSAULTS_PER_TURN);
        if (_locals_spent) {
            if (_local_button.active) {
                _local_button.active = false;
                roster.update_roster();
            }
            _local_button.text_color = c_red;
            _local_button.button_color = c_red;
            _local_button.tooltip = "This planet's forces have already supported the maximum number of ground assaults this turn.";
        }
        _local_button.x1 = _buttons_x;
        _local_button.y1 = _buttons_y;
        _local_button.update();
        _local_button.draw();

        if (_local_button.clicked()) {
            if (_locals_spent) {
                _local_button.active = false;
            }
            roster.update_roster();
        }
    }

    _buttons_y += 30;

    // Ship assault economy: assault-exhausted ships are drawn locked and red (see
    // scr_roster). ToggleButton clicks and Select All still flip their active flag,
    // so force locked ships back off here before the selection is consumed.
    for (var _ls = 0; _ls < array_length(roster.ships); _ls++) {
        var _ls_btn = roster.ships[_ls];
        if (variable_struct_exists(_ls_btn, "assault_locked") && _ls_btn.assault_locked && _ls_btn.active) {
            _ls_btn.active = false;
            roster.ship_multi_selector.changed = true;
        }
    }

    if (roster.ship_multi_selector.changed) {
        roster.update_roster();
    }
    roster.ship_multi_selector.update({x1: _buttons_x, y1: _buttons_y});
    roster.ship_multi_selector.draw();

    draw_set_font(fnt_40k_14);
    draw_set_color(CM_GREEN_COLOR);
    draw_set_alpha(1);
    draw_set_halign(fa_left);

    // Unit types buttons;
    // Dropped from y2 - 220 so the front-width readout under the sector selector has clear
    // air above this header instead of the two lines colliding.
    var _squads_box = {
        header: "Selected Squads:",
        x1: x1 + 40,
        y1: y2 - 196,
    };
    draw_text(_squads_box.x1, _squads_box.y1, _squads_box.header);
    // Spent-ship warning: troops locked aboard ships that already supported their maximum
    // assaults this turn cannot join, and before this line the launch simply proceeded
    // with whatever remained (on a follow-up assault, often just the planetside foothold)
    // and fed it to a full region garrison: the "all forces present but only some fight,
    // unpreventable losses" report. Painted red beside the header so it cannot be missed.
    var _stranded = roster.spent_ship_stranded_count();
    if (_stranded > 0) {
        draw_set_color(c_red);
        draw_text(_squads_box.x1 + string_width(_squads_box.header) + 14, _squads_box.y1, $"{_stranded} unit(s) locked aboard spent ships cannot join this turn");
        draw_set_color(CM_GREEN_COLOR);
    }
    var _x_offset = 0;
    var _row = 0;
    var loop_cycle = array_length(roster.squad_buttons);
    if (array_length(roster.vehicle_buttons) > 0) {
        loop_cycle += array_length(roster.vehicle_buttons);
    }
    var _squad_length = array_length(roster.squad_buttons);
    var _button;
    for (var i = 0; i < loop_cycle; i++) {
        if (i < _squad_length) {
            _button = roster.squad_buttons[i];
        } else {
            _button = roster.vehicle_buttons[i - _squad_length];
        }

        if (_x_offset + _button.w > 590) {
            _row++;
            _x_offset = 0;
        }
        _button.x1 = _squads_box.x1 + _x_offset;
        _button.y1 = (_squads_box.y1 + string_height(_squads_box.header) + 10) + _row * 28;
        _button.update();
        _button.draw();

        if (_button.clicked()) {
            roster.update_roster();
        }

        _x_offset += _button.w + 10;
    }

    // Target
    var race_quantity = 0;
    if (purge == eDROP_TYPE.RAIDATTACK) {
        var target_race = "";
        var target_threat = "";
        // Ported from upstream: without this declaration, attacking a world with no
        // enemy forces left (race_quantity 0) skips the only assignment below and the
        // string_width read throws "not set before reading it".
        var _target_str = "No Target";

        if (attacking >= 5 && attacking <= 13) {
            race_quantity = race_quantities[attacking - 4];
            target_race = races[attacking - 4];
        }

        if (race_quantity >= 1 && race_quantity <= 6) {
            target_threat = threat_levels[race_quantity];
        } else if (race_quantity >= 6) {
            target_threat = threat_levels[6];
        }

        if (race_quantity != 0) {
            _target_str = $"{target_race} ({target_threat})";
        }

        btn_target.x1 = x2 - 50 - string_width(_target_str);
        btn_target.y1 = btn_formation.y2 + 10;
        btn_target.button_color = CM_GREEN_COLOR;
        btn_target.text_color = CM_GREEN_COLOR;
        btn_target.update({str1: _target_str});
        btn_target.draw();
        btn_target.active = force_present[1] != 0;

        // Hold Ground toggle (ground assaults only): placed to the LEFT of the enemy
        // faction/threat label on the same row, out of the ship-selection column where it
        // used to overlap the ship names. When active, survivors stay planetside as a
        // foothold after the battle.
        if (attack == 1) {
            var _hg_button = roster.hold_ground_button;
            _hg_button.update({str1: "Hold Ground"});
            _hg_button.x1 = btn_target.x1 - _hg_button.w - 20;
            _hg_button.y1 = btn_target.y1;
            _hg_button.tooltip = "HOLD GROUND: your surviving troops stay on the surface in the region you attack, instead of returning to orbit. They hold that territory and an automatic battle is fought there each turn until the region is cleared - either they take it, or they are wiped out. Use 'Recall All' in Manage Units to bring them back to their ships. Landing under fire like this is the only way onto an enemy-held region.";
            _hg_button.update();
            _hg_button.draw();
            // ToggleButton.clicked() already flips `active`; toggling again here cancelled it,
            // so the button appeared to do nothing on click.
            _hg_button.clicked();
        }

        if (btn_target.clicked()) {
            var _current_i = 0;
            for (var i = 1; i <= 20; i++) {
                if (force_present[i] == attacking) {
                    _current_i = i;
                    break;
                }
            }
            for (var i = _current_i + 1; i <= 20; i++) {
                if (force_present[i] != 0) {
                    attacking = force_present[i];
                    break;
                }
            }
            if (attacking == force_present[_current_i]) {
                for (var i = 1; i <= 20; i++) {
                    if (force_present[i] != 0) {
                        attacking = force_present[i];
                        break;
                    }
                }
            }
        }

        draw_sprite(spr_faction_icons, attacking, x2 - 100, y1 + 20);

        // Target SECTOR selector: which planetary region the assault lands on. Cycles the planet's
        // conquest focus (shared with the system-view regions panel). Only on multi-region worlds.
        if (planet_region_count(p_target, planet_number) > 1) {
            var _seci = region_focus_get(p_target, planet_number);
            var _secr = region_get(p_target, planet_number, _seci);
            var _forti_n = [
                "None",
                "Sparse",
                "Light",
                "Moderate",
                "Heavy",
                "Major",
                "Extreme",
            ];
            var _sector_str = $"Sector {_seci + 1}: {_secr.name} ({region_faction_name(_secr.owner)}, Fort {_forti_n[clamp(_secr.fortification, 0, 6)]}, Def {_secr.defences})";
            // Show the commitment tradeoff while cycling: outlying sectors of a foe
            // holding several regions meet a partial force; the capital (or a foe
            // in a single region) meets everything, leaders included.
            if (_secr.is_capital || (array_length(regions_owned_by(p_target, planet_number, attacking)) <= 1)) {
                _sector_str += " [full enemy force]";
            } else {
                _sector_str += " [partial enemy force]";
            }
            // Front line: say why a sector cannot be struck, so cycling past a locked one
            // reads as a campaign rule rather than a dead click.
            if (!region_can_assault_index(p_target, planet_number, _seci)) {
                _sector_str += region_owner_is_friendly(p_target, planet_number, _seci) ? " [HELD]" : " [NO FRONT]";
            }
            // Warn before committing: cornered on their final ground, the foe fights with
            // everything left on the world instead of holding a reserve back.
            if ((_secr.owner == attacking) && (planet_faction_last_region(p_target, planet_number, attacking) == _seci)) {
                _sector_str += " [LAST STAND]";
            }
            // Drawn directly (not via InteractiveButton, whose width-based text padding pushes a
            // wide label to the box bottom): a centred box with the text centred both ways inside it.
            // Click cycles the conquest focus (shared with the system-view regions panel).
            draw_set_font(fnt_40k_14);
            var _ssw = string_width(_sector_str);
            var _ssh = string_height(_sector_str);
            var _ssx1 = x3 - (_ssw / 2) - 8;
            // Sit in the empty band between the ship list above and the "Selected Squads:"
            // header below (which is at y2 - 220), rather than up in the ship area. Anchored
            // off y2 so it tracks the squads header and never overlaps the ships.
            var _ssy2 = (y2 - 220) - 14;
            var _ssy1 = _ssy2 - _ssh - 8;
            var _ssx2 = x3 + (_ssw / 2) + 8;
            draw_set_color(CM_GREEN_COLOR);
            draw_rectangle(_ssx1, _ssy1, _ssx2, _ssy2, true);
            draw_set_halign(fa_center);
            draw_set_valign(fa_middle);
            draw_text(x3, (_ssy1 + _ssy2) / 2, _sector_str);
            draw_set_halign(fa_left);
            draw_set_valign(fa_top);
            // Combat width: the ground itself limits how many troops either side can hold in
            // contact, so the shape of the fight is readable before committing and an
            // oversized force is visibly surplus rather than silently wasted.
            // What the ground will actually let the DEFENDER field here: the narrower the
            // front, the smaller the battle, whatever their reserve. The chapter's own
            // number is shown beside it for scale and is never capped by the line.
            var _front_w = region_front_width(p_target, planet_number, _seci);
            var _front_garr = region_garrison(p_target, planet_number, _seci, _secr.owner);
            var _front_engaged = (_front_garr > 0) ? min(_front_w, _front_garr) : _front_w;
            var _front_str = $"Front ({region_terrain(p_target, planet_number, _seci)}): up to {scr_display_number(_front_engaged)} can hold the line here | you commit {scr_display_number(roster.selected_count())}";
            draw_text(_ssx1, _ssy2 + 6, _front_str);
            draw_set_color(CM_GREEN_COLOR);
            if (scr_hit(_ssx1, _ssy1, _ssx2, _ssy2) && mouse_button_clicked()) {
                // Cycle to the next sector the front actually allows, so the selector only
                // lands on a region the assault can be launched at. Falls back to the plain
                // next index when nothing is assaultable, leaving the launch gate to explain.
                var _region_count = planet_region_count(p_target, planet_number);
                var _new_focus = (_seci + 1) % _region_count;
                for (var _try = 0; _try < _region_count; _try++) {
                    var _cand = (_seci + 1 + _try) % _region_count;
                    if (region_can_assault_index(p_target, planet_number, _cand)) {
                        _new_focus = _cand;
                        break;
                    }
                }
                region_focus_set(p_target, planet_number, _new_focus);
                // Stash on the persistent controller too, so the choice survives to launch even if
                // the star's focus is rebuilt in between (the star-state churn that landed troops
                // in the capital regardless of selection).
                obj_controller.pending_battle_region = _new_focus;
                LOGGER.info($"SECTOR SELECTOR click: was {_seci} -> set {_new_focus} (stashed on controller)");
            }
        }
    }

    // Back / Purge buttons
    btn_back.x1 = x3 - 100;
    btn_back.y1 = y2 - 60;
    btn_back.update();
    btn_back.draw();
    if (btn_back.clicked()) {
        menu = 0;
        purge = 0;
        instance_destroy();
    }

    // Behead the Warboss (§16f): only in the raid screen, and only when this world actually has an Ork
    // Warboss present. A decapitation strike — kills the boss (a non-duel death), throwing the clans into a
    // succession scramble or civil war. Spends the fleet's action, like a raid.
    if ((purge == eDROP_TYPE.RAIDATTACK) && planet_feature_bool(p_target.p_feature[planet_number], eP_FEATURES.ORKWARBOSS)) {
        btn_behead.x1 = btn_back.x1;
        btn_behead.y1 = btn_back.y1 - 45;
        btn_behead.active = true;
        btn_behead.update();
        btn_behead.draw();
        if (btn_behead.clicked()) {
            if (sh_target != noone) {
                sh_target.acted += 1;
            }
            var _bh_res = ork_decapitation_strike(p_target, planet_number);
            scr_popup("Decapitation Strike", _bh_res.text, "waaagh");
            menu = 0;
            purge = 0;
            instance_destroy();
        }
    }

    // Attack / Raid buttons
    btn_attack.x1 = btn_back.x1 + btn_attack.width + 10;
    btn_attack.y1 = btn_back.y1;
    if (purge == eDROP_TYPE.RAIDATTACK) {
        btn_attack.str1 = (attack) ? "ATTACK!" : "RAID!";
        btn_attack.active = roster.selected_count() > 0 && race_quantity > 0;
    } else if (purge > 1) {
        btn_attack.str1 = "PURGE";
        btn_attack.active = roster.selected_count() > 0;
    }
    btn_attack.update();
    btn_attack.draw();
    if (btn_attack.clicked()) {
        if (purge == 0) {
            combating = 1; // Start battle here

            // Hardening: resolve the chosen formation through a single bounds-checked read so a
            // bad formation_current cannot crash the drop launch. Falls back to formation 0 when
            // no formations are available.
            var _chosen_form = 0;
            if (array_length(formation_possible) > 0) {
                _chosen_form = formation_possible[clamp(formation_current, 0, array_length(formation_possible) - 1)];
            }

            if (attack == 1) {
                obj_controller.last_attack_form = _chosen_form;
            }
            if (attack == 0) {
                obj_controller.last_raid_form = _chosen_form;
            }

            // The fleet action tick used to run AFTER instance_deactivate_all, writing
            // to a deactivated instance by id. Whether that write lands is
            // runtime-dependent, and the tester's repro (unlimited raids from a
            // stationary fleet, third raid never blocked) matches it silently failing
            // in the compiled build: acted never climbed, so the raid gate
            // (acted <= 1) always passed. Ticked before deactivation instead, with a
            // proof line for the session log.
            if (sh_target != noone) {
                sh_target.acted += 1;
                LOGGER.info($"DROP LAUNCH {((attack == 1) ? "attack" : "raid")}: fleet acted now {sh_target.acted}");
                LOGGER.info($"DROP ROSTER: {roster.selected_count()} unit(s) launching, {roster.spent_ship_stranded_count()} locked aboard spent ships, {array_length(roster.full_roster_units)} left behind in total");
            }

            instance_deactivate_all(true);
            instance_activate_object(obj_controller);
            instance_activate_object(obj_ini);
            instance_activate_object(obj_drop_select);

            // Ship assault economy: each distinct ship contributing units to this
            // ground deployment spends one support use this turn (SHIP_ASSAULTS_PER_TURN
            // max). This now covers raids as well as attacks (both are RAIDATTACK drops
            // that land troops from ships), so a raid is gated per ship like an assault
            // rather than by the fleet-wide acted counter. fleet.acted above still ticks
            // for movement and the unconverted purge gate. Local planetside forces
            // (ship id -1) cost nothing here; they spend a local use just below.
            if (purge == eDROP_TYPE.RAIDATTACK) {
                var _spent_ships = [];
                var _local_participated = false;
                for (var _su = 0; _su < array_length(roster.selected_units); _su++) {
                    var _sel = roster.selected_units[_su];
                    var _sel_ship = is_struct(_sel) ? _sel.ship_location : obj_ini.veh_lid[_sel[0]][_sel[1]];
                    if (_sel_ship > -1) {
                        if (!array_contains(_spent_ships, _sel_ship)) {
                            array_push(_spent_ships, _sel_ship);
                            var _drop_kind = (attack == 1) ? "assault" : "raid";
                            ship_action_spend(_sel_ship, _drop_kind);
                            LOGGER.info($"{string_upper(_drop_kind)} SPEND ship {_sel_ship}: uses now {ship_action_used(_sel_ship, _drop_kind)}/{SHIP_ASSAULTS_PER_TURN}");
                        }
                    } else {
                        _local_participated = true;
                    }
                }
                // Planetside forces joining the assault spend one of the planet's
                // local support uses, so troops cannot be dropped onto the surface
                // and used for unlimited free attacks.
                if (_local_participated) {
                    local_assault_spend(p_target, planet_number);
                }
            }

            if ((attacking == 10) || (attacking == 11)) {
                remove_planet_problem(planet_number, "meeting", p_target);
                remove_planet_problem(planet_number, "meeting_trap", p_target);
            }

            instance_create(0, 0, obj_ncombat);
            obj_ncombat.battle_object = p_target;
            obj_ncombat.battle_loc = p_target.name;
            obj_ncombat.battle_id = planet_number;
            obj_ncombat.dropping = 1 - attack;
            obj_ncombat.attacking = attack;
            obj_ncombat.enemy = attacking;
            obj_ncombat.formation_set = _chosen_form;
            obj_ncombat.defending = false;
            obj_ncombat.local_forces = roster.local_button.active;
            // Foothold: only a ship-launched ground assault (attack, fleet present) can hold
            // ground; local-only or reinforcement battles do not embark/disembark here.
            obj_ncombat.hold_ground = ((attack == 1) && (sh_target != noone) && roster.hold_ground_button.active) ? 1 : 0;
            LOGGER.info($"HOLD GROUND launch: attack={attack} sh_target_ok={(sh_target != noone)} button_active={roster.hold_ground_button.active} -> hold_ground={obj_ncombat.hold_ground} | battle_region={obj_ncombat.battle_region} focus={region_focus_get(p_target, planet_number)} planet={planet_number} regions={planet_region_count(p_target, planet_number)}");

            // Orbital Gun Array toll: a ship-launched assault against a gun-world provokes
            // the guns unless it targets the safe landing region. Only applies when a fleet
            // is actually in orbit (a purely planetside local-forces attack risks no ship).
            // The toll is charged once per launch on the region the assault is aimed at.
            if (instance_exists(sh_target)) {
                var _og_region = (planet_region_count(p_target, planet_number) > 1) ? region_focus_get(p_target, planet_number) : -1;
                orbital_gun_ship_toll(p_target, planet_number, _og_region);
            }

            // (Imperial Guard assault bring-along disabled for now: the player-side
            //  battlefield unit needs real per-model data, so this is being rebuilt.
            //  Until then we do not touch the embarked Guard, so attacks cost nothing.)
            obj_ncombat.player_attack_guard = 0;

            // Note: the region the player is pushing into is derived by the conquest overlay
            // (region_assault_target / regions_sync), which handles per-region defence resistance
            // and consume-on-capture without touching the fragile combat core. The tactical
            // obj_ncombat fortification system assumes the PLAYER is the defender, so it is left
            // alone here; making the battle screen itself region-aware is the deferred Option B.

            // Region commitment: assaulting an OUTLYING sector of a multi-region world
            // meets only that region's GARRISON (its capped slice, see region_garrison);
            // the capital, or a foe squeezed into one region, meets the reserve/whole
            // force. Leaders (Warboss, Farseer) only defend the capital.
            var _region_partial = false;
            if ((attack == 1) && (planet_region_count(p_target, planet_number) > 1)) {
                var _rp_focus = region_focus_get(p_target, planet_number);
                var _rp_region = region_get(p_target, planet_number, _rp_focus);
                var _rp_is_capital = is_struct(_rp_region) && _rp_region.is_capital;
                if (!_rp_is_capital) {
                    var _rp_held = array_length(regions_owned_by(p_target, planet_number, attacking));
                    if (_rp_held > 1) {
                        _region_partial = true;
                    }
                }
            }
            obj_ncombat.region_partial = _region_partial;
            // Capture the exact region the assault targets, so the foothold lands there regardless
            // of any later focus change. -1 on a single-region world (whole-planet battle).
            if (planet_region_count(p_target, planet_number) > 1) {
                // Prefer the region stashed on the persistent controller at selector-click time; it
                // survives the star-state churn that was resetting the live focus before launch.
                var _pbr = obj_controller.pending_battle_region;
                var _valid_pbr = is_real(_pbr) && (_pbr >= 0) && (_pbr < planet_region_count(p_target, planet_number));
                obj_ncombat.battle_region = _valid_pbr ? _pbr : region_focus_get(p_target, planet_number);
            } else {
                obj_ncombat.battle_region = -1;
            }
            LOGGER.info($"BATTLE_REGION captured: {obj_ncombat.battle_region} (pending={obj_controller.pending_battle_region}, focus={region_focus_get(p_target, planet_number)}, planet={planet_number}, regions={planet_region_count(p_target, planet_number)})");

            var _planet = obj_ncombat.battle_object.p_feature[obj_ncombat.battle_id];
            if (obj_ncombat.battle_object.space_hulk == 1) {
                obj_ncombat.battle_special = "space_hulk";
            }
            if ((planet_feature_bool(_planet, eP_FEATURES.WARLORD6) == 1) && (obj_ncombat.enemy == eFACTION.ELDAR) && (obj_controller.faction_defeated[6] == 0) && !_region_partial) {
                obj_ncombat.leader = 1;
            }
            if (obj_ncombat.enemy == eFACTION.ORK && planet_feature_bool(_planet, eP_FEATURES.ORKWARBOSS) && !_region_partial) {
                obj_ncombat.leader = 1;
                obj_ncombat.ork_warboss = _planet[search_planet_features(_planet, eP_FEATURES.ORKWARBOSS)[0]];
            }

            if ((obj_ncombat.enemy == eFACTION.TYRANIDS) && (obj_ncombat.battle_object.space_hulk == 0)) {
                if (has_problem_planet(planet_number, "tyranid_org", p_target)) {
                    obj_ncombat.battle_special = "tyranid_org";
                }
            }

            if (obj_ncombat.enemy == eFACTION.HERETICS) {
                if (planet_feature_bool(obj_ncombat.battle_object.p_feature[obj_ncombat.battle_id], eP_FEATURES.CHAOSWARBAND) == 1) {
                    obj_ncombat.battle_special = "ChaosWarband";
                    obj_ncombat.leader = 1;
                }
            }

            var _threats = [
                0,
                0,
                0,
                0,
                0,
                sisters,
                eldar,
                ork,
                tau,
                tyranids,
                traitors,
                chaos,
                demons,
                necrons,
            ];
            if (obj_ncombat.enemy >= eFACTION.ECCLESIARCHY && obj_ncombat.enemy <= eFACTION.NECRONS) {
                obj_ncombat.threat = _threats[obj_ncombat.enemy];
            }

            if (obj_ncombat.enemy == eFACTION.TAU) {
                var eth = scr_quest(4, "ethereal_capture", 8, 0);
                if ((eth > 0) && (obj_ncombat.battle_object.p_owner[obj_ncombat.battle_id] == eFACTION.TAU)) {
                    var rolli;
                    rolli = irandom_range(1, 100);
                    if ((obj_ncombat.threat == 6) && (rolli <= 80)) {
                        obj_ncombat.ethereal = 1;
                    }
                    if ((obj_ncombat.threat == 5) && (rolli <= 65)) {
                        obj_ncombat.ethereal = 1;
                    }
                    if ((obj_ncombat.threat == 4) && (rolli <= 50)) {
                        obj_ncombat.ethereal = 1;
                    }
                    if ((obj_ncombat.threat == 3) && (rolli <= 35)) {
                        obj_ncombat.ethereal = 1;
                    }
                }
            }

            if ((obj_ncombat.threat > 1) && (obj_ncombat.battle_special != "ChaosWarband") && (attack == 0)) {
                obj_ncombat.threat -= 1;
            }
            if (obj_ncombat.threat < 1) {
                obj_ncombat.threat = 1;
            }
            if ((obj_ncombat.enemy == eFACTION.CHAOS) && (obj_ncombat.battle_object.p_type[obj_ncombat.battle_id] == "Daemon")) {
                obj_ncombat.threat = 7;
            }

            // Outlying-sector commitment: derive the engaged force level from the
            // committed share of the real headcount where the population is
            // modelled; otherwise knock two levels off. Only level-scale battles
            // (1-6) qualify; Enormicus (7) and Imperium headcount battles pass.
            if (_region_partial && (obj_ncombat.threat >= 1) && (obj_ncombat.threat <= 6)) {
                // Engage the focused region's actual GARRISON (capped per-region force),
                // not a flat fraction of the planet total, so an outlying region fields its
                // stationed slice and the capital its reserve. See region_garrison.
                var _rp_focus2 = region_focus_get(p_target, planet_number);
                var _rp_garrison = region_garrison(p_target, planet_number, _rp_focus2, attacking);
                if ((count_to_level_anchors(attacking) != -1) && (_rp_garrison > 0)) {
                    obj_ncombat.threat = max(1, count_to_level(attacking, _rp_garrison));
                } else {
                    obj_ncombat.threat = max(1, obj_ncombat.threat - 2);
                }
            }

            var _battle_place = obj_ncombat.battle_object;
            var _battle_sub_loc = obj_ncombat.battle_id;
            var _chaos_lord_jump_possible = attacking == 0 || attacking == 10 || attacking == 11;
            var _no_know_chaos = _battle_place.p_traitors[_battle_sub_loc] == 0 && _battle_place.p_chaos[_battle_sub_loc] == 0;

            var _chaos_warlord_present = planet_feature_bool(_battle_place.p_feature[obj_ncombat.battle_id], eP_FEATURES.WARLORD10);

            var _chaos_popup_turn_reached = obj_controller.turn >= obj_controller.chaos_turn;

            var _chaos_unknown = (obj_controller.known[eFACTION.CHAOS] == 0) && (obj_controller.faction_gender[10] == 1);

            if (_chaos_lord_jump_possible && _no_know_chaos) {
                if (_chaos_popup_turn_reached && _chaos_warlord_present) {
                    if (_chaos_unknown) {
                        var pop;
                        pop = instance_create(0, 0, obj_popup);
                        pop.image = "chaos_symbol";
                        pop.title = "Concealed Heresy";
                        pop.text = $"Your astartes set out and begin to cleanse {planet_numeral_name(_battle_sub_loc, _battle_place)} of possible heresy.  The general populace appears to be devout in their faith, but a disturbing trend appears- the odd citizen cursing your forces, frothing at the mouth, and screaming out heresy most foul.  One week into the cleansing a large hostile force is detected approaching and encircling your forces.";
                        cancel_combat();
                        combating = 0;
                        instance_activate_all();
                        exit;
                    }
                    if (obj_controller.known[eFACTION.CHAOS] >= 2 && obj_controller.faction_gender[10] == 1) {
                        with (obj_drop_select) {
                            obj_ncombat.enemy = eFACTION.HERETICS;
                            obj_ncombat.threat = 0;
                            cancel_combat();
                            combating = 0;
                            instance_destroy();
                            instance_activate_all();
                            exit;
                        }
                    }
                }
            }

            scr_battle_allies();
            setup_battle_formations();
            roster.add_to_battle();

            // The grid takes over from obj_ncombat's own Step event, so every
            // battle reaches it and not just this one. All this path adds is a
            // better field size than threat alone can give: the assault knows
            // the region's real front width.
            if (grid_combat_enabled()) {
                var _gfront = region_front_width(p_target, planet_number, region_focus_get(p_target, planet_number));
                obj_ncombat.grid_width = clamp(round(_gfront / 200), 8, 32);
                // Terrain decides what is standing on the battlefield. Read here
                // because this path knows which region is being assaulted; every
                // other spawner falls back to open ground.
                var _greg = region_get(p_target, planet_number, region_focus_get(p_target, planet_number));
                if (is_struct(_greg)) {
                    if (variable_struct_exists(_greg, "name")) {
                        obj_ncombat.grid_terrain = grid_terrain_from_region_name(_greg.name);
                    }
                    if (variable_struct_exists(_greg, "is_capital")) {
                        obj_ncombat.grid_capital = _greg.is_capital;
                    }
                }
            }
        } else if (purge > 1) {
            draw_set_alpha(0.2);
            draw_rectangle(954, 556, 1043, 579, 0);
            draw_set_alpha(1);
            var _purge_score = 0;
            if (purge == eDROP_TYPE.PURGEBOMBARD) {
                _purge_score = roster.purge_bombard_score();
            }

            if (purge >= eDROP_TYPE.PURGEFIRE) {
                _purge_score = roster.selected_count();
            }

            var _p_data = p_target.system_datas[planet_number];

            _p_data.refresh_data();

            _p_data.purge(purge, _purge_score);

            // Cleanse by Fire ALSO scours a Fungal Bloom if one has taken root here (§16h): the same
            // promethium that burns out heretics and xenos torches the Ork spore-bed — removes the bloom
            // feature and most of the greenskin horde. (Behead's old standalone "Cleanse" button was removed.)
            if ((purge == eDROP_TYPE.PURGEFIRE) && _p_data.has_feature(eP_FEATURES.FUNGAL_BLOOM)) {
                var _cleanse_res = ork_cleanse_bloom(p_target, planet_number);
                scr_popup("Cleanse by Fire", _cleanse_res.text, "");
            }

            // Bombardment grinds down the TARGETED sector's own defences (region-level), matching the
            // sector shown/selected on the bombard screen. Guarded so old saves / no-region worlds
            // are untouched.
            if ((purge == eDROP_TYPE.PURGEBOMBARD) && variable_instance_exists(p_target, "p_regions")) {
                var _bombsec = region_focus_get(p_target, planet_number);
                var _bombrgn = region_get(p_target, planet_number, _bombsec);
                _bombrgn.fortification = max(0, _bombrgn.fortification - 1);
                if (_bombrgn.defences > 0) {
                    _bombrgn.defences = max(0, _bombrgn.defences - 1);
                }
            }
        }
    }
}

function drop_select_draw() {
    with (obj_drop_select) {
        if (purge != eDROP_TYPE.PURGESELECT) {
            drop_select_unit_selection();
        }

        // Purge shit happens bellow;
        // God, save us;
        if (menu == eMENU.DEFAULT) {
            if (purge == 1) {} else if (purge >= 2) {
                draw_set_halign(fa_center);
                draw_set_font(fnt_40k_30b);

                // 2 is bombardment

                var x2 = 535;
                var y2 = 200;

                draw_set_halign(fa_left);
                draw_set_color(c_gray);
                var _purge_strings = [
                    "Bombard Purging {0}",
                    "Fire Cleansing {0}",
                    "Selective Purging {0}",
                    "Assassinate Governor ({0})",
                ];
                var _planet_string = planet_numeral_name(planet_number, p_target);
                draw_text_transformed(x2 + 14, y2 + 12, string(_purge_strings[purge - 2], _planet_string), 0.6, 0.6, 0);

                // Disposition here
                var pp = planet_number;

                var succession = has_problem_planet(pp, "succession", p_target);

                if (((p_target.dispo[pp] >= 0) && (p_target.p_owner[pp] <= eFACTION.ECCLESIARCHY) && (p_target.p_population[pp] > 0)) && (!succession)) {
                    var wack = 0;
                    draw_set_color(c_blue);
                    draw_rectangle(x2 + 12, y2 + 53, x2 + 12 + max(0, (min(100, p_target.dispo[pp]) * 4.37)), y2 + 71, 0);
                }
                draw_set_color(c_gray);
                draw_rectangle(x2 + 12, y2 + 53, x2 + 449, y2 + 71, 1);
                draw_set_color(c_white);

                draw_set_font(fnt_40k_14b);
                draw_set_halign(fa_center);
                if (!succession) {
                    if ((p_target.dispo[pp] >= 0) && (p_target.p_first[pp] <= eFACTION.ECCLESIARCHY) && (p_target.p_owner[pp] <= eFACTION.ECCLESIARCHY) && (p_target.p_population[pp] > 0)) {
                        draw_text(x2 + 231, y2 + 54, string_hash_to_newline("Disposition: " + string(min(100, p_target.dispo[pp])) + "/100"));
                    }
                    if ((p_target.dispo[pp] > -30) && (p_target.dispo[pp] < 0) && (p_target.p_owner[pp] <= eFACTION.ECCLESIARCHY) && (p_target.p_population[pp] > 0)) {
                        draw_text(x2 + 231, y2 + 54, string_hash_to_newline("Disposition: ???/100"));
                    }
                    if (((p_target.dispo[pp] >= 0) && (p_target.p_first[pp] <= eFACTION.ECCLESIARCHY) && (p_target.p_owner[pp] > eFACTION.ECCLESIARCHY)) || (p_target.p_population[pp] <= 0)) {
                        draw_text(x2 + 231, y2 + 54, string_hash_to_newline("-------------"));
                    }
                    if (p_target.dispo[pp] <= -3000) {
                        draw_text(x2 + 231, y2 + 54, "Chapter Rule");
                    }
                }
                if (succession == 1) {
                    draw_text(x2 + 231, y2 + 54, "War of Succession");
                }

                draw_set_color(c_gray);
                draw_set_font(fnt_40k_14);
                draw_set_halign(fa_left);

                // Planet icon here
                draw_rectangle(x2 + 459, y2 + 14, x2 + 516, y2 + 71, 0);

                // Target SECTOR for the bombardment (the region whose defences it grinds down).
                if (planet_region_count(p_target, planet_number) > 1) {
                    var _bseci = region_focus_get(p_target, planet_number);
                    var _bsecr = region_get(p_target, planet_number, _bseci);
                    var _bforti_n = [
                        "None",
                        "Sparse",
                        "Light",
                        "Moderate",
                        "Heavy",
                        "Major",
                        "Extreme",
                    ];
                    var _bsector_str = $"Sector {_bseci + 1}: {_bsecr.name} ({region_faction_name(_bsecr.owner)}, Fort {_bforti_n[clamp(_bsecr.fortification, 0, 6)]}, Def {_bsecr.defences})";
                    // Drawn directly (centred both ways), below the purge option buttons.
                    draw_set_font(fnt_40k_14);
                    var _bcx = x2 + 230;
                    var _bsw = string_width(_bsector_str);
                    var _bsh = string_height(_bsector_str);
                    var _bsx1 = _bcx - (_bsw / 2) - 8;
                    var _bsy1 = y2 + 340;
                    var _bsx2 = _bcx + (_bsw / 2) + 8;
                    var _bsy2 = _bsy1 + _bsh + 8;
                    draw_set_color(CM_GREEN_COLOR);
                    draw_rectangle(_bsx1, _bsy1, _bsx2, _bsy2, true);
                    draw_set_halign(fa_center);
                    draw_set_valign(fa_middle);
                    draw_text(_bcx, (_bsy1 + _bsy2) / 2, _bsector_str);
                    draw_set_halign(fa_left);
                    draw_set_valign(fa_top);
                    if (scr_hit(_bsx1, _bsy1, _bsx2, _bsy2) && mouse_button_clicked()) {
                        region_focus_set(p_target, planet_number, (_bseci + 1) % planet_region_count(p_target, planet_number));
                    }
                }

                draw_set_font(fnt_40k_14);
                draw_set_color(c_gray);
                draw_set_alpha(1);

                var smin, smax;
                var w;
                w = -1;
                smin = 0;
                smax = 0;

                //draw_text(x2 + 14, y2 + 352, string_hash_to_newline("Selection: " + string(smin) + "/" + string(smax)));
            }
        }
    }
}

/// @self Asset.GMObject.obj_drop_select
function collect_local_units() {
    //
    // I think this script is used to count local forces. l_ meaning local.
    //
    ship_use[500] = 0;
    ship_max[500] = l_size;
    purge_d = ship_max[500];

    if (purge == 1) {
        if (sh_target != noone) {
            max_ships = sh_target.capital_number + sh_target.frigate_number + sh_target.escort_number;

            if (sh_target.acted >= 1) {
                instance_destroy();
            }

            var tump;
            tump = 0;

            var i, q, b;
            i = -1;
            q = -1;
            b = -1;
            repeat (sh_target.capital_number) {
                b += 1;
                if (sh_target.capital[b] != "") {
                    i += 1;
                    ship[i] = sh_target.capital[i];

                    ship_use[i] = 0;
                    tump = sh_target.capital_num[i];
                    ship_max[i] = obj_ini.ship_carrying[tump];
                    ship_ide[i] = tump;

                    ship_size[i] = 3;

                    purge_a += 3;
                    purge_b += ship_max[i];
                    purge_c += ship_max[i];
                    purge_d += ship_max[i];
                }
            }
            q = -1;
            repeat (sh_target.frigate_number) {
                q += 1;
                if (sh_target.frigate[q] != "") {
                    i += 1;
                    ship[i] = sh_target.frigate[q];

                    ship_use[i] = 0;
                    tump = sh_target.frigate_num[q];
                    ship_max[i] = obj_ini.ship_carrying[tump];
                    ship_ide[i] = tump;

                    ship_size[i] = 2;

                    purge_a += 1;
                    purge_b += ship_max[i];
                    purge_c += ship_max[i];
                    purge_d += ship_max[i];
                }
            }
            q = -1;
            repeat (sh_target.escort_number) {
                q += 1;
                if ((sh_target.escort[q] != "") && (obj_ini.ship_carrying[sh_target.escort_num[q]] > 0)) {
                    i += 1;
                    ship[i] = sh_target.escort[q];

                    ship_use[i] = 0;
                    tump = sh_target.escort_num[q];
                    ship_max[i] = obj_ini.ship_carrying[tump];
                    ship_ide[i] = tump;

                    ship_size[i] = 1;

                    purge_b += ship_max[i];
                    purge_c += ship_max[i];
                    purge_d += ship_max[i];
                }
            }
        }

        if (p_target.p_player[planet_number] > 0) {
            max_ships += 1;
        }
        var pp = planet_number;
        purge_d = p_target.p_type[pp] != "Dead";

        if (has_problem_planet(pp, "succession", p_target)) {
            purge_d = 0;
        }

        if (p_target.dispo[pp] < -2000) {
            purge_d = 0;
        }

        if ((planet_feature_bool(p_target.p_feature[pp], eP_FEATURES.MONASTERY) == 1) && (obj_controller.homeworld_rule != 1)) {
            purge_d = 0;
        }

        if (p_target.p_type[pp] == "Dead") {
            purge_d = 0;
        }
    }
}
