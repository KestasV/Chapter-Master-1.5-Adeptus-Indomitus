/// @self Asset.GMObject.obj_pnunit
function add_marines_to_recovery() {
    var _roles = active_roles();
    for (var i = 0; i < array_length(unit_struct); i++) {
        var _unit = unit_struct[i];
        if (is_struct(_unit) && ally[i] == false) {
            if (marine_dead[i] == 1 && marine_type[i] != "") {
                var _role_priority_bonus = 0;
                var _chief_librarian = $"Chief {_roles[eROLE.LIBRARIAN]}";
                switch (_unit.role()) {
                    case obj_ini.role[100][eROLE.CHAPTERMASTER]:
                        _role_priority_bonus = 720;
                        break;
                    case "Forge Master":
                    case "Master of Sanctity":
                    case "Master of the Apothecarion":
                    case _chief_librarian:
                        _role_priority_bonus = 360;
                        break;
                    case _roles[eROLE.CAPTAIN]:
                    case _roles[eROLE.HONOURGUARD]:
                    case _roles[eROLE.ANCIENT]:
                        _role_priority_bonus = 160;
                        break;
                    case _roles[eROLE.VETERANSERGEANT]:
                    case _roles[eROLE.TERMINATOR]:
                        _role_priority_bonus = 80;
                        break;
                    case _roles[eROLE.VETERAN]:
                    case _roles[eROLE.SERGEANT]:
                    case _roles[eROLE.CHAMPION]:
                    case _roles[eROLE.CHAPLAIN]:
                    case _roles[eROLE.APOTHECARY]:
                    case _roles[eROLE.TECHMARINE]:
                    case _roles[eROLE.LIBRARIAN]:
                    case "Codiciery":
                    case "Lexicanum":
                        _role_priority_bonus = 40;
                        break;
                    case _roles[eROLE.TACTICAL]:
                    case _roles[eROLE.ASSAULT]:
                    case _roles[eROLE.DEVASTATOR]:
                        _role_priority_bonus = 20;
                        break;
                    case _roles[eROLE.SCOUT]:
                    default:
                        _role_priority_bonus = 0;
                        break;
                }

                var _priority = _unit.experience + _role_priority_bonus;
                var _recovery_candidate = {
                    "id": i,
                    "unit": _unit,
                    "column_id": id,
                    "priority": _priority,
                };

                ds_priority_add(obj_ncombat.marines_to_recover, _recovery_candidate, _recovery_candidate.priority);
            }
        }
    }
}

/// @self Asset.GMObject.obj_pnunit
function add_vehicles_to_recovery() {
    var _vehicles_priority = {
        "Land Raider": 10,
        "Predator": 5,
        "Whirlwind": 4,
        "Rhino": 3,
        "Land Speeder": 3,
        "Leman Russ": 3,
        "Chimera": 2,
        "Bike": 1,
    };

    for (var i = 0; i < array_length(veh_dead); i++) {
        if (veh_dead[i] && !veh_ally[i] && veh_type[i] != "") {
            var _priority = 1;
            if (struct_exists(_vehicles_priority, veh_type[i])) {
                _priority = _vehicles_priority[$ veh_type[i]];
            }

            var _recovery_candidate = {
                "id": i,
                "column_id": id,
                "priority": _priority,
            };

            ds_priority_add(obj_ncombat.vehicles_to_recover, _recovery_candidate, _recovery_candidate.priority);
        } else {
            continue;
        }
    }
}

/// @self Asset.GMObject.obj_pnunit
function assemble_alive_units() {
    for (var i = 0; i < array_length(unit_struct); i++) {
        var _unit = unit_struct[i];
        if (is_struct(_unit) && ally[i] == false) {
            if (!marine_dead[i]) {
                array_push(obj_ncombat.end_alive_units, _unit);
            }
        }
    }
}

function distribute_experience(_units, _total_exp) {
    var _unit_count = array_length(_units);
    var _exp_reward = 0;
    var _exp_reward_max = 5;
    var _unit_exp_ceiling = 200;
    var _exp_mod_min = 0.1;

    if (_unit_count > 0 && _total_exp > 0) {
        _exp_reward = min(_total_exp / _unit_count, _exp_reward_max);
        for (var i = 0; i < _unit_count; i++) {
            var _unit = _units[i];
            var _exp_mod = max(1 - (_unit.experience / _unit_exp_ceiling), _exp_mod_min);
            var _exp_update_data = _unit.add_exp(_exp_reward * _exp_mod);

            var _powers_learned = _exp_update_data[1];
            if (_powers_learned > 0) {
                array_push(obj_ncombat.upgraded_librarians, _unit);
            }
        }
    }

    return _exp_reward;
}

function after_battle_slime_and_equipment_maintenance(unit) {
    if (unit.base_group == "astartes") {
        if (unit.gene_seed_mutations.mucranoid == 1) {
            var muck = roll_dice_unit(unit, 1, 100, "high");
            if (muck == 1) {
                //slime  armour damaged due to mucranoid
                if (unit.armour != "") {
                    obj_controller.specialist_point_handler.add_to_armoury_repair(unit.armour());
                    obj_ncombat.mucra[unit.company] = 1;
                    obj_ncombat.slime += unit.get_armour_data("maintenance");
                }
            }
        }
    }
}

function check_for_plasma_bomb_and_tomb(unit) {
    if (obj_ncombat.plasma_bomb || obj_ncombat.defeat) {
        return;
    }
    var _star = obj_ncombat.battle_object;
    var _planet = obj_ncombat.battle_id;
    var _necron_strength = _star.p_necrons[_planet];
    if (unit.gear() == "Plasma Bomb" && !string_count("mech_tomb2", obj_ncombat.battle_special)) {
        if (obj_ncombat.enemy == eFACTION.NECRONS && awake_tomb_world(_star.p_feature[_planet])) {
            if (((_necron_strength - 2) < 3 && obj_ncombat.dropping) || (_necron_strength - 1) < 3) {
                obj_ncombat.plasma_bomb += 1;
                unit.update_gear("", false, false);
            }
        }
    }
}

/// @self Asset.GMObject.obj_pnunit
function after_battle_part2() {
    var _unit;

    for (var i = 0; i < array_length(unit_struct); i++) {
        _unit = unit_struct[i];
        if (!marine_dead[i] && marine_type[i] == "Death Company") {
            if (_unit.role() != "Death Company") {
                _unit.update_role("Death Company");
            }
        }

        if (!marine_dead[i] && !ally[i]) {
            after_battle_slime_and_equipment_maintenance(_unit);

            check_for_plasma_bomb_and_tomb(_unit);

            if ((_unit.gear() == "Exterminatus") && (obj_ncombat.dropping != 0) && (obj_ncombat.defeat == 0)) {
                if (obj_ncombat.exterminatus == 0) {
                    obj_ncombat.exterminatus += 1;
                    _unit.update_gear("", false, false);
                }
                // obj_ncombat.exterminatus+=1;scr_add_item("Exterminatus",1);
                // _unit.gear()="";
            }
        }

        var destroy = 0;
        if ((marine_dead[i] || obj_ncombat.defeat != 0) && !ally[i]) {
            after_combat_recover_marine_gene_seed(_unit);
            after_combat_dead_marine_equipment_recovered(_unit);
        }
    }

    for (var i = 0; i < array_length(veh_dead); i++) {
        if (((veh_dead[i] == 1) || (obj_ncombat.defeat != 0)) && (veh_type[i] != "") && (veh_ally[i] == false)) {
            obj_ncombat.vehicle_deaths += 1;

            var _vehicle_type = veh_type[i];
            if (!struct_exists(obj_ncombat.vehicles_lost_counts, _vehicle_type)) {
                obj_ncombat.vehicles_lost_counts[$ _vehicle_type] = 1;
            } else {
                obj_ncombat.vehicles_lost_counts[$ _vehicle_type]++;
            }

            // Determine which companies to crunch
            obj_ncombat.crunch[veh_co[i]] = 1;
        }
    }
}

/// @self Asset.GMObject.obj_pnunit
function after_battle_part1() {
    var unit;
    var skill_level;
    for (var i = 0; i < array_length(unit_struct); i++) {
        unit = unit_struct[i];
        if (!is_struct(unit)) {
            continue;
        }
        if ((marine_type[i] != "") && (unit.hp() < -3000) && (obj_ncombat.defeat == 0)) {
            marine_dead[i] = 0;
            //unit.add_or_sub_health(5000);
        } // For incapitated

        if (ally[i] == false) {
            if ((obj_ncombat.dropping == 1) && (obj_ncombat.defeat == 1) && (marine_dead[i] < 2)) {
                marine_dead[i] = 1;
            }
            if ((obj_ncombat.dropping == 0) && (obj_ncombat.defeat == 1) && (marine_dead[i] < 2)) {
                marine_dead[i] = 2;
                marine_hp[i] = -50;
            }

            if ((marine_type[i] != "") && (obj_ncombat.defeat == 1) && (marine_dead[i] < 2)) {
                marine_dead[i] = 1;
                marine_hp[i] = -50;
            }
            if ((i < array_length(veh_type)) && (veh_type[i] != "") && (obj_ncombat.defeat == 1)) {
                veh_dead[i] = 1;
                veh_hp[i] = -200;
            }

            if (!marine_dead[i]) {
                // Apothecaries for saving marines;
                if (unit.IsSpecialist(SPECIALISTS_APOTHECARIES, true)) {
                    skill_level = unit.intelligence * 0.0125;
                    if (marine_gear[i] == "Narthecium") {
                        skill_level *= 2;
                        obj_ncombat.apothecaries_alive++;
                    }
                    skill_level += random(unit.luck * 0.05);
                    obj_ncombat.unit_recovery_score += skill_level;
                }

                // Techmarines for saving vehicles;
                if (unit.IsSpecialist(SPECIALISTS_TECHS, true)) {
                    skill_level = unit.technology / 10;
                    skill_level += random(unit.luck / 2);
                    skill_level += unit.gear_special_value("combi_tool");
                    obj_ncombat.vehicle_recovery_score += round(skill_level);
                    obj_ncombat.techmarines_alive++;
                }
            }
        }
    }
}

function after_combat_recover_marine_gene_seed(unit) {
    var comm = false;
    if (unit.IsSpecialist(SPECIALISTS_STANDARD, true)) {
        obj_ncombat.final_command_deaths += 1;
        var recent = true;
        if (is_specialist(unit.role, SPECIALISTS_TRAINEES)) {
            recent = false;
        } else if (array_contains([string("Venerable {0}", obj_ini.role[100][6]), "Codiciery", "Lexicanum"], unit.role())) {
            recent = false;
        }
        if (recent == true) {
            scr_recent($"death_{unit.name_role()}");
        }
    } else {
        obj_ncombat.final_marine_deaths += 1;
    }
    // obj_ncombat.final_marine_deaths+=1;

    // show_message("ded; increase final deaths");

    if (obj_controller.blood_debt == 1) {
        if (unit.role() == obj_ini.role[100][eROLE.SCOUT]) {
            obj_controller.penitent_current += 2;
        } else {
            obj_controller.penitent_current += 4;
        }
        obj_controller.penitent_turn = 0;
        obj_controller.penitent_turnly = 0;
    }

    if (unit.base_group == "astartes") {
        var _birthday = unit.age();
        var _current_year = (obj_controller.millenium * 1000) + obj_controller.year;
        var _seed_harvestable = 0;
        var _seed_lost = 0;

        if (_birthday <= (_current_year - 10) && unit.gene_seed_mutations.zygote == 0) {
            _seed_lost++;
            if (irandom_range(1, 10) > 1) {
                _seed_harvestable++;
            }
        }
        if (_birthday <= (_current_year - 5)) {
            _seed_lost++;
            if (irandom_range(1, 10) > 1) {
                _seed_harvestable++;
            }
        }

        obj_ncombat.seed_harvestable += _seed_harvestable;
        obj_ncombat.seed_lost += _seed_lost;
    }

    var last = 0;

    var _unit_role = unit.role();
    if (!struct_exists(obj_ncombat.units_lost_counts, _unit_role)) {
        obj_ncombat.units_lost_counts[$ _unit_role] = 1;
    } else {
        obj_ncombat.units_lost_counts[$ _unit_role]++;
    }

    // Determine which companies to crunch
    obj_ncombat.crunch[unit.company] = 1;
}

function after_combat_dead_marine_equipment_recovered(unit) {
    var _equipment = unit.unit_equipment_data();

    var _equip_slots = _equipment.present_items;

    var basic_recover_chance = 40;

    if (scr_has_adv("Scavangers")) {
        basic_recover_chance += 10;
    }
    if (!obj_ncombat.defending) {
        basic_recover_chance -= 10;
    }
    if (obj_ncombat.dropping == 1) {
        if (scr_has_adv("Lightning Warriors")) {
            basic_recover_chance -= 10;
        } else {
            basic_recover_chance -= 25;
        }
    }

    for (var i = 0; i < array_length(_equip_slots); i++) {
        var _recover = true;
        var _slot = _equip_slots[i];
        var _item = _equipment.get_item(_slot)

        var _specific_item_chance = roll_dice_chapter(1, 100, "low");

        if (obj_ncombat.dropping && obj_ncombat.defeat) {
            _specific_item_chance = 9999;
        }
        //if (obj_ini.race[marine_co[i], marine_id[i]]!=1) then _specific_item_chance=9999;

        var _specific_type_recovery = basic_recover_chance + _item.recovery_chance;

        if (_item.is_artifact && _specific_type_recovery < 90) {
            _specific_type_recovery = 95;
        }

        if (_item.name == "Exterminatus") {
            if (obj_ncombat.defeat == 0) {
                _specific_item_chance = 0;
                if (obj_ncombat.dropping != 0) {
                    obj_ncombat.exterminatus += 1;
                }
            }
            if (obj_ncombat.defeat) {
                _specific_item_chance = 9999;
            }
        }

        if (_specific_item_chance > _specific_type_recovery) {
            _recover = false;
            if (!_item.is_artifact) {
                obj_ncombat.post_equipment_lost.add_item(_item.name, _item.quality, unit.uid);
            }
        } else {
            if (!_item.is_artifact) {
                obj_ncombat.post_equipment_recovered.add_item(_item.name, _item.quality, unit.uid);
            }
        }

        switch (_slot) {
            case "armour":
                unit.update_armour("", false, _recover);
            case "wep1":
                unit.weapon_one("", false, _recover);
            case "wep2":
                unit.update_weapon_two("", false, _recover);
            case "gear":
                unit.update_gear("", false, _recover);
            case "mobi":
                unit.update_mobility_item("", false, _recover);
        }
    }
}

/// @self Asset.GMObject.obj_pnunit
/// @function hold_ground_disembark
/// @description When the assault was launched with Hold Ground set, the surviving attackers
///              (non-ally, non-local, still alive) STAY planetside as a foothold instead of
///              returning to orbit: each is unloaded onto the battle world (ship_location -1,
///              added to p_player), so the world's contested auto-battle engages them each
///              turn until the player Recalls them. Local forces were already planetside and
///              are untouched. Runs only on a won or survived ground battle.
function hold_ground_disembark() {
    if (!instance_exists(obj_ncombat) || (obj_ncombat.hold_ground != 1)) {
        return;
    }
    var _star = obj_ncombat.battle_object;
    var _planet = obj_ncombat.battle_id;
    if (!instance_exists(_star)) {
        return;
    }
    var _landed = 0;
    var _p_before = _star.p_player[_planet];
    // The region that was assaulted is where the foothold forms. Use the region captured at battle
    // launch (obj_ncombat.battle_region); fall back to the live focus only if it was not set.
    var _land_region = obj_ncombat.battle_region;
    if (!is_real(_land_region) || (_land_region < 0) || (_land_region >= planet_region_count(_star, _planet))) {
        _land_region = region_focus_get(_star, _planet);
    }
    var _region_force_added = 0;
    var _n_us = array_length(unit_struct);
    for (var i = 0; i < _n_us; i++) {
        var _unit = unit_struct[i];
        if (!is_struct(_unit)) {
            continue;
        }
        // Land every surviving attacker that is not an ally and not dead. marine_local is NOT a
        // filter here: both add_unit_to_battle call sites pass is_local=true, so it is true for
        // every battle unit and never meant "already planetside" - skipping on it lands nobody.
        if (ally[i] == true) {
            continue;
        }
        if (marine_dead[i] == 1) {
            continue;
        }
        // Idempotence: a unit already standing on this world stays where it is. Covers
        // local planetside forces that joined the assault (already counted into the
        // foothold store by their original landing) and any unit the fill registered
        // into more than one block. The Molech log landed 444 units from a 345-marine
        // assault and 44 vehicles from 26 fielded; every duplicate re-added its full
        // size to the region force (p_player 0 -> 970).
        var _now = _unit.marine_location();
        if ((_now[0] == eLOCATION_TYPES.PLANET) && (_now[1] == _planet)) {
            continue;
        }
        // Opposed landing: place the unit planetside DIRECTLY (bypassing the gated regular
        // unload(), since Hold Ground is precisely how you land under fire on enemy soil). Handle
        // the ship_carrying bookkeeping ourselves so nothing is lost.
        _unit.set_last_ship();
        var _prev_loc = _unit.marine_location();
        if ((_prev_loc[0] == eLOCATION_TYPES.SHIP) && (_prev_loc[1] >= 0) && (_prev_loc[1] < array_length(obj_ini.ship_carrying))) {
            _unit.get_unit_size();
            obj_ini.ship_carrying[_prev_loc[1]] = max(0, obj_ini.ship_carrying[_prev_loc[1]] - _unit.size);
        }
        _unit.ship_location = -1;
        _unit.location_string = _star.name;
        _unit.planet_location = _planet;
        _unit.region_location = _land_region; // the assaulted region becomes the unit's foothold
        _unit.get_unit_size();
        _region_force_added += _unit.size;
        _landed++;
    }
    // Land this block's surviving vehicles too: the armour that fought the assault holds
    // the ground with the infantry. Only ship-borne, player-owned, living vehicles land.
    // A vehicle disabled in the fight returns to the fleet for repair (Alarm_5's techmarine
    // recovery runs after this), mirroring how critically injured marines are evacuated.
    // Local garrison armour (veh_wid already set) was never aboard and is untouched.
    var _veh_landed = 0;
    for (var _vi = 1; _vi <= veh; _vi++) {
        if (veh_dead[_vi]) {
            continue;
        }
        if (veh_ally[_vi]) {
            continue;
        }
        var _vc = veh_co[_vi];
        var _vv = veh_id[_vi];
        if (obj_ini.veh_role[_vc][_vv] == "") {
            continue;
        }
        if (obj_ini.veh_wid[_vc][_vv] != 0) {
            continue;
        }
        var _vship = obj_ini.veh_lid[_vc][_vv];
        if ((_vship < 0) || (_vship >= array_length(obj_ini.ship_carrying))) {
            continue;
        }
        var _vsize = scr_unit_size("", obj_ini.veh_role[_vc][_vv], true);
        // Remember the ship it launched from BEFORE clearing veh_lid, so Recall All can
        // send it back to its own transport.
        set_vehicle_last_ship([_vc, _vv], false);
        obj_ini.ship_carrying[_vship] = max(0, obj_ini.ship_carrying[_vship] - _vsize);
        obj_ini.veh_loc[_vc][_vv] = _star.name;
        obj_ini.veh_lid[_vc][_vv] = -1;
        obj_ini.veh_wid[_vc][_vv] = _planet;
        obj_ini.veh_uid[_vc][_vv] = 0;
        _region_force_added += _vsize;
        _veh_landed++;
    }
    // Deposit the whole landed force into the TARGETED region, then set p_player from the
    // per-region sum. region_player_force_add re-syncs p_player to the sum of footholds, which
    // becomes the authoritative planet total and supersedes the per-unit p_player writes unload()
    // may have made (so the force is counted once, and now has a region location).
    region_player_force_add(_star, _planet, _land_region, _region_force_added);
    LOGGER.info($"HOLD GROUND disembark on {_star.name} {_planet} [{formation_display_name(formation_type)}]: {_landed} of {_n_us} landed into region {_land_region} with {_veh_landed} vehicle(s), region force +{_region_force_added}, p_player {_p_before} -> {_star.p_player[_planet]}");
}
